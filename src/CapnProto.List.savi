:trait box _FromStructPointer
  :new box read_from_pointer(pointer CapnProto.Pointer.Struct)

:struct box CapnProto.List(A _FromStructPointer)
  :let _p CapnProto.Pointer.StructList
  :new box read_from_pointer(@_p)

  :fun size: @_p._list_count.usize

  :fun "[]!"(n): A.read_from_pointer(@_p[n]!)

  :fun each
    :yields A
    @_p.each -> (p | yield A.read_from_pointer(p))

:trait _FromStructPointerBuilder
  :new from_pointer(pointer CapnProto.Pointer.Struct.Builder)

:struct CapnProto.List.Builder(A _FromStructPointerBuilder)
  :let _p CapnProto.Pointer.StructList.Builder
  :new from_pointer(@_p)

  :fun size: @_p._list_count.usize

  :fun ref "[]!"(n): A.from_pointer(@_p[n]!)

  :fun ref each
    :yields A
    @_p.each -> (p | yield A.from_pointer(p))
